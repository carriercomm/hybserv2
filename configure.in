dnl $Id$
dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.50)
AC_INIT(Hybserv2, testing, kreator|AT|srce|hr)
AC_CONFIG_HEADER(include/defs.h)

dnl Get revision number
if test -f "$srcdir/.svn/entries" ; then
  REVISION=`grep revision $srcdir/.svn/entries | sort -ur | head -n 1 | cut -d '"' -f 2`
else
  REVISION="unknown"
fi
AC_SUBST(REVISION)

dnl Set language and save it
AC_LANG_C

dnl Get system type
AC_CANONICAL_HOST
MYHOST=$host_os
case "$host_os" in
*sunos*|*solaris*)
  AC_DEFINE_UNQUOTED(HAVE_SOLARIS, , [Define to 1 if you have Solaris OS])
  ;;
*cygwin)
  AC_DEFINE_UNQUOTED(HAVE_CYGWIN, , [Define to 1 if you have Cygwin environment])
esac

dnl Standard installation path
AC_PREFIX_DEFAULT(/usr/local)

dnl Compiler environment
AC_PROG_CC
AC_ISC_POSIX
AC_C_INLINE
AC_C_CONST
AC_SYS_LARGEFILE

dnl Checks for programs.
AC_PROG_MAKE_SET
AC_PROG_INSTALL
AC_PATH_PROG(RM, rm)
AC_PATH_PROG(CP, cp)
AC_PATH_PROG(LN, ln)
AC_PATH_PROG(LS, ls)
AC_PATH_PROG(CHMOD, chmod)
AC_PATH_PROG(CHOWN, chown)

dnl Who am i
WHOAMI=`whoami`
AC_SUBST(WHOAMI)

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_TIME
AC_HEADER_SYS_WAIT
AC_HEADER_DIRENT
AC_HEADER_STAT
AC_CHECK_HEADERS(errno.h assert.h netdb.h fcntl.h signal.h \
  arpa/inet.h sys/socket.h netinet/in.h sys/resource.h sys/signal.h)

dnl Add standard prefix to fix broken environments
LIBS="${LIBS}"
CFLAGS="${CFLAGS}"

dnl Checks for library functions.
AC_TYPE_SIGNAL
AC_CHECK_FUNCS(strchr memcpy strdup strerror strncpy gettimeofday)
AC_CHECK_LIB(nsl, gethostbyname)
AC_CHECK_LIB(socket, socket)
AC_CHECK_LIB(crypt, crypt)

dnl Try dmalloc support
AC_ARG_ENABLE(dmalloc, [  --enable-dmalloc        Enable dmalloc support [default=no]])
if test "x$enable_dmalloc" = "xyes"; then
  AC_CHECK_LIB(dmalloc, malloc)
fi

AC_OUTPUT(Makefile src/Makefile tools/Makefile \
  bin/settings.conf bin/cleandb include/config.h)

echo "
Configuration:
  Source revision:  ${REVISION}
  Detected OS:      ${host_os}
  Install path:     ${prefix}/hybserv
  Compiler:         ${CC}
  Compiler flags:   ${CFLAGS}
  Linker flags:     ${LIBS}

NOTE: Please edit include/config.h to change the settings
to match your network before running make!"
