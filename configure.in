dnl $Id$
dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.50)
AC_COPYRIGHT([$Id$])
AC_INIT([Hybserv2], [testing])
AC_CONFIG_HEADER(include/defs.h)
AC_GNU_SOURCE

dnl Get revision number
if test -f "$srcdir/.svn/entries" ; then
  REVISION=`grep revision $srcdir/.svn/entries | sort -ur | head -n 1 | cut -d '"' -f 2`
else
  REVISION="unknown"
fi
AC_SUBST(REVISION)

dnl Set language and save it
AC_LANG_C

dnl Get system type
AC_CANONICAL_HOST
MYHOST=$host_os
case "$host_os" in
*sunos*|*solaris*)
  AC_DEFINE_UNQUOTED(HAVE_SOLARIS, , [Define to 1 if you have Solaris OS])
  ;;
*cygwin)
  AC_DEFINE_UNQUOTED(HAVE_CYGWIN, , [Define to 1 if you have Cygwin environment])
esac

dnl Standard installation path
AC_PREFIX_DEFAULT(/usr/local)

dnl Compiler environment
AC_PROG_CC
AC_ISC_POSIX
AC_C_INLINE
AC_C_CONST
AC_SYS_LARGEFILE
AC_C_BIGENDIAN

dnl Checks for programs.
AC_PROG_MAKE_SET
AC_PROG_INSTALL
AC_PATH_PROG(RM, rm)
AC_PATH_PROG(CP, cp)
AC_PATH_PROG(MV, mv)
AC_PATH_PROG(LN, ln)
AC_PATH_PROG(TOUCH, touch)

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_TIME
AC_HEADER_SYS_WAIT
AC_HEADER_DIRENT
AC_HEADER_STAT
AC_CHECK_HEADERS([errno.h assert.h netdb.h fcntl.h signal.h \
  arpa/inet.h sys/socket.h netinet/in.h sys/resource.h sys/signal.h])

dnl Checks for types.
AC_CHECK_TYPE(socklen_t, ,
  [AC_DEFINE([socklen_t], [unsigned int],
  [If we don't have a real socklen_t, unsigned int is good enough.])],
  [#include <sys/types.h>
#include <sys/socket.h>])

dnl Checks for library functions.
AC_TYPE_SIGNAL
AC_CHECK_FUNCS([strchr memcpy strdup strerror strncpy gettimeofday])
AC_SEARCH_LIBS(gethostbyname, nsl, ,
  [AC_MSG_ERROR([You have no gethostbyname()! Aborting.])])
AC_SEARCH_LIBS(socket, socket, ,
  [AC_MSG_ERROR([You have no socket()! Aborting.])])
AC_SEARCH_LIBS(crypt, [crypt descrypt])

dnl Try dmalloc support
AC_ARG_ENABLE(dmalloc, [  --enable-dmalloc        Enable dmalloc support [default=no]])
if test "x$enable_dmalloc" = "xyes"; then
  AC_CHECK_LIB(dmalloc, malloc)
fi

AC_OUTPUT(Makefile src/Makefile tools/Makefile \
  bin/settings.conf bin/cleandb include/config.h)

echo "
Configuration:
  Source revision:    ${REVISION}
  Detected OS:        ${host_os}
  Install path:       ${prefix}/hybserv
  Compiler:           ${CC}
  Compiler flags:     ${CFLAGS}
  Preprocessor:       ${CPP}
  Preprocessor flags: ${CPPFLAGS}
  Linker flags:       ${LDFLAGS}
  Libraries to link:  ${LIBS}

NOTE: Please edit include/config.h to change the settings
to match your network before running make!"
